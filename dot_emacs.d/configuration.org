#+TITLE: Emacs Configuration
#+AUTHOR: Zach
#+EMAIL:
#+OPTIONS: toc:nil num:nil
This is a test org config file that's a frankenstein from
 https://github.com/hrs/dotfiles/blob/main/emacs/dot-emacs.d/configuration.org
which is much more organized and thought out.

* Configure =use-package=

I use =use-package= to install and configure my packages. My =init.el= includes
the initial setup for =package.el= and ensures that =use-package= is installed,
since I wanna do that right away.

This makes sure that =use-package= will install the package if it's not already
available. It also means that I should be able to open Emacs for the first time
on a fresh Debian box and have my whole environment automatically installed. I'm
not /totally/ sure about that, but we're gettin' close.

#+begin_src emacs-lisp
  (require 'use-package-ensure)
  (setq use-package-always-ensure t)
#+end_src

Always compile packages, and use the newest version available.

#+begin_src emacs-lisp
  (use-package auto-compile
    :config (auto-compile-on-load-mode))
  (setq load-prefer-newer t)
#+end_src

* Set personal information

** Who am I? Where am I?

#+begin_src emacs-lisp
  (setq user-full-name "Zachary Bombay"
        user-mail-address "zacharybombay@gmail.com"
        calendar-latitude 0.0
        calendar-longitude 0.0
        calendar-location-name "")
#+end_src

# * Add =resources= to =load-path=

# #+begin_src emacs-lisp
#   (add-to-list 'load-path "~/.emacs.d/resources/")
# #+end_src

* Utility functions

Define a big ol' bunch of handy utility functions.

#+begin_src emacs-lisp
  (defun zb/rename-file (new-name)
    (interactive "FNew name: ")
    (let ((filename (buffer-file-name)))
      (if filename
    (progn
      (when (buffer-modified-p)
        (save-buffer))
      (rename-file filename new-name t)
      (kill-buffer (current-buffer))
      (find-file new-name)
      (message "Renamed '%s' -> '%s'" filename new-name))
  (message "Buffer '%s' isn't backed by a file!" (buffer-name)))))

  (defun zb/append-to-path (path)
    "Add a path both to the $PATH variable and to Emacs' exec-path."
    (setenv "PATH" (concat (getenv "PATH") ":" path))
    (add-to-list 'exec-path path))

  (defun zb/notify-send (title message)
    "Display a desktop notification by shelling out to `notify-send'."
    (call-process-shell-command
     (format "notify-send -t 2000 \"%s\" \"%s\"" title message)))
#+end_src

* UI preferences

** UTF-8 complience

#+begin_src emacs-lisp
  (prefer-coding-system 'utf-8)
  (set-default-coding-systems 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (setq-default buffer-file-coding-system 'utf-8)
#+end_src

** Tweak window chrome

I don't usually use the menu or scroll bar, and they take up useful space.

#+begin_src emacs-lisp
  (tool-bar-mode 0)
  (menu-bar-mode 0)
  (scroll-bar-mode -1)
  ;  (global-linum-mode t)
  (column-number-mode t)
  (setq inhibit-startup-message t)
  (global-visual-line-mode)
  (setq frame-title-format "%b")
;;  (setq frame-title-format '((:eval (projectile-project-name))))
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src

** Ido mode

#+begin_src emacs-lisp
  (setq ido-enable-flex-matching t)
  (setq ido-everywhere t)
  (ido-mode 1)
#+end_src

** Use fancy lambdas

Why not?

#+begin_src emacs-lisp
  (global-prettify-symbols-mode t)
#+end_src

** Highlight the current line

=global-hl-line-mode= softly highlights the background color of the line
containing point. It makes it a bit easier to find point, and it's useful when
pairing or presenting code.

#+begin_src emacs-lisp
  (global-hl-line-mode)
#+end_src

** Load up a theme
#+begin_src emacs-lisp
  (use-package tron-legacy-theme)
  (load-theme 'tron-legacy t)
#+end_src

    # abyss-theme
    # underwater-theme
    # challenger-deep-theme
    # cyberpunk-theme
    # dakrone-theme
    # dracula-theme
    # espresso-theme
    # exotica-theme

** Use =moody= for a beautiful modeline

This gives me a truly lovely ribbon-based modeline.

#+begin_src emacs-lisp
    (use-package moody
      :config
      (setq x-underline-at-descent-line t
	    moody-mode-line-height 30)
    (moody-replace-mode-line-buffer-identification))
;;    (moody-replace-vc-mode))
#+end_src

** Use =minions= to hide all minor modes

I never want to see a minor mode, and manually adding =:diminish= to every
use-package declaration is a hassle. This uses =minions= to hide all the minor
modes in the modeline. Nice!

By default there's a =;-)= after the major mode; that's an adorable default, but
I'd rather skip it.

#+begin_src emacs-lisp
   (use-package minions
     :config
     (setq minions-mode-line-lighter ""
           minions-mode-line-delimiters '("" . ""))
     (minions-mode 1))
#+end_src

** Set default font and configure font resizing

I'm partial to Source Code Pro.

The standard =text-scale-= functions just resize the text in the current buffer;
I'd generally like to resize the text in /every/ buffer, and I usually want to
change the size of the modeline, too (this is especially helpful when
presenting). These functions and bindings let me resize everything all together!

Note that this overrides the default font-related keybindings from
=sensible-defaults=.

#+begin_src emacs-lisp
  (setq zb/default-font "Source Code Pro")
  (setq zb/default-font-size 12)
  (setq zb/current-font-size zb/default-font-size)

  (setq zb/font-change-increment 1.1)

  (defun zb/font-code ()
    "Return a string representing the current font (like \"Source Code Pro-14\")."
    (concat zb/default-font "-" (number-to-string zb/current-font-size)))

  (defun zb/set-font-size ()
    "Set the font to `zb/default-font' at `zb/current-font-size'.
  Set that for the current frame, and also make it the default for
  other, future frames."
    (let ((font-code (zb/font-code)))
      (if (assoc 'font default-frame-alist)
	  (setcdr (assoc 'font default-frame-alist) font-code)
	(add-to-list 'default-frame-alist (cons 'font font-code)))
      (set-frame-font font-code)))

  (defun zb/reset-font-size ()
    "Change font size back to `zb/default-font-size'."
    (interactive)
    (setq zb/current-font-size zb/default-font-size)
    (zb/set-font-size))

  (defun zb/increase-font-size ()
    "Increase current font size by a factor of `zb/font-change-increment'."
    (interactive)
    (setq zb/current-font-size
	  (ceiling (* zb/current-font-size zb/font-change-increment)))
    (zb/set-font-size))

  (defun zb/decrease-font-size ()
    "Decrease current font size by a factor of `zb/font-change-increment', down to a minimum size of 1."
    (interactive)
    (setq zb/current-font-size
	  (max 1
	  (floor (/ zb/current-font-size zb/font-change-increment))))
    (zb/set-font-size))

  (define-key global-map (kbd "C-)") 'zb/reset-font-size)
  (define-key global-map (kbd "C-+") 'zb/increase-font-size)
  (define-key global-map (kbd "C--") 'zb/decrease-font-size)
  (zb/reset-font-size)
#+end_src


** Highlight uncommitted changes

Use the =diff-hl= package to highlight changed-and-uncommitted lines when
programming.

#+begin_src emacs-lisp
  (use-package diff-hl
    :config
    (add-hook 'prog-mode-hook 'turn-on-diff-hl-mode)
    (add-hook 'vc-dir-mode-hook 'turn-on-diff-hl-mode))
#+end_src

* Programming Project Management

I use a few packages in virtually every programming or writing environment to
manage the project, handle auto-completion, search for terms, and deal with
version control. That's all in here.

** =company=

Use =company-mode= everywhere.

#+begin_src emacs-lisp
  (use-package company)
  (add-hook 'after-init-hook 'global-company-mode)
#+end_src

Use =M-/= for completion.

#+begin_src emacs-lisp
  (global-set-key (kbd "M-/") 'company-complete-common)
#+end_src

** =flycheck=

I'd like to enable flycheck all kinds of places.

#+begin_src emacs-lisp
  (use-package let-alist)
  (use-package flycheck
    :hook ruby-mode)
#+end_src

** =magit=

I use =magit= to handle version control. It's lovely, but I tweak a few things:

- I bring up the status menu with =C-x g=.
- Use =evil= keybindings with =magit=.
- The default behavior of =magit= is to ask before pushing. I haven't had any
  problems with accidentally pushing, so I'd rather not confirm that every time.
- Per [[http://tbaggery.com/2008/04/19/a-note-about-git-commit-messages.html][tpope's suggestions]], highlight commit text in the summary line that goes
  beyond 50 characters.
- I'd like to start in the insert state when writing a commit message.

#+begin_src emacs-lisp
  (use-package magit
    :bind
    ("C-x g" . magit-status)

    :config
    (use-package with-editor)

    (setq magit-push-always-verify nil
	  git-commit-summary-max-length 50))
#+end_src


* Programming environments

I like shallow indentation, but tabs are displayed as 8 characters by default.
This reduces that.

#+begin_src emacs-lisp
  (setq-default tab-width 2)
#+end_src

Treating terms in CamelCase symbols as separate words makes editing a little
easier for me, so I like to use =subword-mode= everywhere.

#+begin_src emacs-lisp
  (use-package subword
    :config (global-subword-mode 1))
#+end_src

Compilation output goes to the =*compilation*= buffer. I rarely have that window
selected, so the compilation output disappears past the bottom of the window.
This automatically scrolls the compilation window so I can always see the
output.

#+begin_src emacs-lisp
  (setq compilation-scroll-output t)
#+end_src


** Golang

Install =go-mode= and related packages:

#+begin_src emacs-lisp
  (use-package go-mode)
  (use-package go-errcheck)
  (use-package company-go)
#+end_src

Define my =$GOPATH= and tell Emacs where to find the Go binaries.

#+begin_src emacs-lisp
  (setenv "GOPATH" "~/code/go")
  ;; (setenv "GOPATH" "/home/zach/code/go")
  (zb/append-to-path (concat (getenv "GOPATH") "/bin"))
#+end_src

Run =goimports= on every file when saving, which formats the file and
automatically updates the list of imports. This requires that the =goimports=
binary be installed.

#+begin_src emacs-lisp
  (setq gofmt-command "goimports")
  (add-hook 'before-save-hook 'gofmt-before-save)
#+end_src

When I open a Go file,

- Start up =company-mode= with the Go backend. This requires that the =gocode=
  binary is installed,
- Redefine the default =compile= command to something Go-specific, and
- Enable =flycheck=.

#+begin_src emacs-lisp
  (add-hook 'go-mode-hook
            (lambda ()
              (set (make-local-variable 'company-backends)
                   '(company-go))
              (company-mode)
              (if (not (string-match "go" compile-command))
                  (set (make-local-variable 'compile-command)
                       "go build -v && go test -v && go vet"))
              (flycheck-mode)))
#+end_src

** Haskell

# #+begin_src emacs-lisp
#   (use-package haskell-mode)
# #+end_src

# Enable =haskell-doc-mode=, which displays the type signature of a function, and
# use smart indentation.

# #+begin_src emacs-lisp
#   (add-hook 'haskell-mode-hook
#             (lambda ()
#               (haskell-doc-mode)
#               (turn-on-haskell-indent)))
# #+end_src

# #+begin_src emacs-lisp
#   (zb/append-to-path "~/.cabal/bin")
# #+end_src

** JavaScript and CoffeeScript

Install =coffee-mode= from editing CoffeeScript code.

# #+begin_src emacs-lisp
#   (use-package coffee-mode)
# #+end_src

# Indent everything by 2 spaces.

# #+begin_src emacs-lisp
#   (setq js-indent-level 2)

#   (add-hook 'coffee-mode-hook
#             (lambda ()
#               (yas-minor-mode 1)
#               (setq coffee-tab-width 2)))
# #+end_src

** Lisps

*** All Lisps

I like to use =paredit= in Lisp modes to balance parentheses (and more!).

#+begin_src emacs-lisp
  (use-package paredit)
#+end_src

=rainbow-delimiters= is convenient for coloring matching parentheses.

#+begin_src emacs-lisp
  (use-package rainbow-delimiters)
#+end_src

All the lisps have some shared features, so we want to do the same things for
all of them. That includes using =paredit=, =rainbow-delimiters=, and
highlighting the whole expression when point is on a parenthesis.

#+begin_src emacs-lisp
  (setq lispy-mode-hooks
        '(clojure-mode-hook
          emacs-lisp-mode-hook
          lisp-mode-hook
          scheme-mode-hook))

  (dolist (hook lispy-mode-hooks)
    (add-hook hook (lambda ()
                     (setq show-paren-style 'expression)
                     (paredit-mode)
                     (rainbow-delimiters-mode))))
#+end_src

If I'm writing in Emacs lisp I'd like to use =eldoc-mode= to display
documentation.

#+begin_src emacs-lisp
  (use-package eldoc
    :config
    (add-hook 'emacs-lisp-mode-hook 'eldoc-mode))
#+end_src

I also like using =flycheck-package= to ensure that my Elisp packages are
correctly formatted.

#+begin_src emacs-lisp
  (use-package flycheck-package)

  (eval-after-load 'flycheck
    '(flycheck-package-setup))
#+end_src

# ** OCaml

# Use =tuareg-mode= for editing OCaml.

# #+begin_src emacs-lisp
#   (use-package tuareg
#     :config
#     (electric-indent-mode 0))
# #+end_src

# Configure Merlin. This also requires installing the Merlin package through OPAM
# with =opam install merlin=.

# #+begin_src emacs-lisp
#   (use-package merlin
#     :config
#     (add-hook 'tuareg-mode-hook 'merlin-mode t)

#     (with-eval-after-load 'company
#       (add-to-list 'company-backends 'merlin-company-backend))
#     (add-hook 'merlin-mode-hook 'company-mode))
# #+end_src

** Python

Use python-mode package and set python executable path depending on OS.
#+begin_src emacs-lisp
  (use-package python-mode)
  (cond
    ((string-equal system-type "windows-nt")
      (progn
        (zb/append-to-path "C:\\Python38\\Scripts")))
    ((string-equal system-type "gnu/linux")
      (progn
        (zb/append-to-path "/bin"))))
;;        (zb/append-to-path "~/.local/bin"))))
#+end_src

Enable =elpy=. This provides automatic indentation, auto-completion, syntax
checking, etc.

#+begin_src emacs-lisp
  (use-package elpy)
  (elpy-enable)
  (setq python-shell-interpreter "python"
        python-shell-interpreter-args "-i")
#+end_src

Use =flycheck= for syntax checking:

#+begin_src emacs-lisp
  (add-hook 'elpy-mode-hook 'flycheck-mode)
#+end_src

Configure Jedi along with the associated =company= mode:

#+begin_src emacs-lisp
  (use-package company-jedi)
  (add-to-list 'company-backends 'company-jedi)

  (add-hook 'python-mode-hook 'jedi:setup)
  (setq jedi:complete-on-dot t)
#+end_src

** Rust

Use =rust-mode= to edit Rust code.

Run =rustfmt= automatically when saving a file.

#+begin_src emacs-lisp
  (use-package rust-mode
    :config
    (zb/append-to-path "~/.cargo/bin")
    (setq rust-format-on-save t))
#+end_src

** =sh=

Indent with 2 spaces.

#+begin_src emacs-lisp
  (add-hook 'sh-mode-hook
            (lambda ()
              (setq sh-basic-offset 2
                    sh-indentation 2)))
#+end_src

** Scala

# Ensure that =scala-mode= and =sbt-mode= are installed.

# #+begin_src emacs-lisp
#   (use-package scala-mode
#     :interpreter
#     ("scala" . scala-mode))
#   (use-package sbt-mode)
# #+end_src

# Don't show the startup message with launching ENSIME:

# #+begin_src emacs-lisp
#   (setq ensime-startup-notification nil)
# #+end_src

# Bind a few keys to common operations:

# #+begin_src emacs-lisp
#   (evil-define-key 'normal ensime-mode-map (kbd "C-t") 'ensime-type-at-point)
#   (evil-define-key 'normal ensime-mode-map (kbd "M-.") 'ensime-edit-definition)
# #+end_src


** YAML

#+begin_src emacs-lisp
  (use-package yaml-mode)
#+end_src

* Terminal

I add a bunch of hooks to =term-mode=:

- I'd like links (URLs, etc) to be clickable.
- Yanking in =term-mode= doesn't quite work. The text from the paste appears in
  the buffer but isn't sent to the shell process. This correctly binds =C-y= and
  middle-click to yank the way we'd expect.
- I bind =M-o= to quickly change windows. I'd like that in terminals, too.
- I don't want to perform =yasnippet= expansion when tab-completing.

#+begin_src emacs-lisp
  (defun zb/term-paste (&optional string)
    (interactive)
    (process-send-string
     (get-buffer-process (current-buffer))
     (if string string (current-kill 0))))

  (add-hook 'term-mode-hook
            (lambda ()
              (goto-address-mode)
              (define-key term-raw-map (kbd "C-y") 'zb/term-paste)
              (define-key term-raw-map (kbd "<mouse-2>") 'zb/term-paste)
              (define-key term-raw-map (kbd "M-o") 'other-window))
              (setq yas-dont-activate t))
#+end_src

* Publishing and task management with Org-mode

I'd like the initial scratch buffer to be in Org:

#+begin_src emacs-lisp
  (setq initial-major-mode 'org-mode)
#+end_src
** Org-Roam

#+begin_src emacs-lisp
  (use-package org-roam)
  (executable-find "sqlite3")
  (setq org-roam-directory "~/sync/Nextcloud/notes/org-roam")
  (add-hook 'after-init-hook 'org-roam-mode)
#+end_src

** Display preferences

I like to see an outline of pretty bullets instead of a list of asterisks.

#+begin_src emacs-lisp
  (use-package org-bullets
    :init
    (add-hook 'org-mode-hook 'org-bullets-mode))
#+end_src

# I like seeing a little downward-pointing arrow instead of the usual ellipsis
# (=...=) that org displays when there's stuff under a header.

#+begin_src emacs-lisp
  (setq org-ellipsis "⤵")
#+end_src

** Task management and agenda views

Store my org files in =~/sync/Nextcloud/org=, maintain an inbox in Nextcloud, define
the location of an index file (my main todo list), and archive finished tasks in
=~/sync/Nextcloud/org/archive.org=.

#+begin_src emacs-lisp
  (setq org-directory "~/sync/Nextcloud/org")
  ;; (setq org-directory "~/Documents/org")

  (defun org-file-path (filename)
    "Return the absolute address of an org file, given its relative name."
    (concat (file-name-as-directory org-directory) filename))

  (setq org-inbox-file "~/sync/Nextcloud/org/inbox.org")
  (setq org-index-file (org-file-path "index.org"))
  (setq org-archive-location
        (concat (org-file-path "archive.org") "::* From %s"))
#+end_src

I store most of my personal tasks in my index and maintain a separate file for
work-related tasks, so I'd like to derive my agenda from those files. I've also
got some annual OKRs in =goals.org=.

I also keep a schedule in =events.org=. Plus some recurring events in,
reasonably, a =recurring-events.org= file. Those are (mostly) structured as
=org-habit= items so they can recur according to a schedule.

#+begin_src emacs-lisp
  (setq org-agenda-files (list org-index-file
                               (org-file-path "events.org")
                               (org-file-path "goals.org")
                               (org-file-path "recurring-events.org")
                               (org-file-path "work.org")))
#+end_src

#+begin_src emacs-lisp
  (setq org-todo-keywords
        '((sequence "TODO(t)" "|" "DONE(d)")
          (sequence "WAITING(w)" "CANCELED(c)")
          (sequence "ACTIVE(a)")))
  (setq org-todo-keyword-faces
        '(("ACTIVE" . (:foreground "yellow")) ("WAITING" . (:foreground "cyan")) ("CANCELED" . (:foreground "purple" :weight bold))))
#+end_src

Hitting =C-c C-x C-s= will mark a todo as done and move it to an appropriate
place in the archive.

#+begin_src emacs-lisp
  (defun zb/mark-done-and-archive ()
    "Mark the state of an org-mode item as DONE and archive it."
    (interactive)
    (org-todo 'done)
    (org-archive-subtree))

  (define-key org-mode-map (kbd "C-c C-x C-s") 'zb/mark-done-and-archive)
#+end_src

Record the time that a todo was archived.

#+begin_src emacs-lisp
  (setq org-log-done 'time)
#+end_src

Ensure that a task can't be marked as done if it contains unfinished subtasks or
checklist items. This is handy for organizing "blocking" tasks hierarchically.

#+begin_src emacs-lisp
  (setq org-enforce-todo-dependencies t)
  (setq org-enforce-todo-checkbox-dependencies t)
#+end_src

Begin weeks /today/, not on the last Monday.

#+begin_src emacs-lisp
  (setq org-agenda-start-on-weekday nil)
#+end_src

# Hide the category prefix from tasks. In practice, I've usually only got one or
# two files of tasks, so prefixing tasks with the file they're stored in is mostly
# redundant.

# #+begin_src emacs-lisp
#   (setq org-agenda-prefix-format '((agenda . " %i %?-12t% s")
#                                    (todo . " %i ")
#                                    (tags . " %i ")
#                                    (search . " %i ")))
# #+end_src

I use =org=habit= to schedule recurring events and reminders for myself.

#+begin_src emacs-lisp
  (require 'org-habit)
#+end_src

# Don't show the =org-habit= consistency graph. I don't actually find it
# motivating, personally, and it kinda visually fills up my agenda. There's no
# provided way to disable the graph, as far as I can tell, so I've just redefined
# the function to do nothing.

# #+begin_src emacs-lisp
#   (defun org-habit-build-graph (habit starting current ending)
#     "                             ")
#   (setq org-habit-graph-column 60)
# #+end_src

I've got some custom agenda views that I use to visualize what I'm working on.

The "Personal agenda" view is a bit complicated; it displays:

1. My agenda for the next few days, including any recurring habits or scheduled
   events,
2. All the other, not-high-priority non-habit TODO items,
3. All the PENDING tasks, which are awaiting a response from someone else but
   still require my attention (I might need to ping someone, say), and
4. All the BLOCKED tasks, which are pending other tasks (but might be worth
   keeping an eye on anyway).

I effectively use the "A" priority to decide where I want to focus my attention
on a given day (I'll assign those priorities the night before, if I'm really on
top of things), so displaying them at the top makes sense.

A lot of this is based on [[https://blog.aaronbieber.com/2016/09/24/an-agenda-for-life-with-org-mode.html][Aaron Bieber's agenda configuration]], including the
=skip-subtree-if-priority= and =skip-subtree-if-habit= functions, which I've
shamelessly stolen and re-prefixed.

#+begin_src emacs-lisp
  (defun zb/org-skip-subtree-if-priority (priority)
    "Skip an agenda subtree if it has a priority of PRIORITY.

  PRIORITY may be one of the characters ?A, ?B, or ?C."
    (let ((subtree-end (save-excursion (org-end-of-subtree t)))
          (pri-value (* 1000 (- org-lowest-priority priority)))
          (pri-current (org-get-priority (thing-at-point 'line t))))
      (if (= pri-value pri-current)
          subtree-end
        nil)))

  (defun zb/org-skip-subtree-if-habit ()
    "Skip an agenda entry if it has a STYLE property equal to \"habit\"."
    (let ((subtree-end (save-excursion (org-end-of-subtree t))))
      (if (string= (org-entry-get nil "STYLE") "habit")
          subtree-end
        nil)))

  (setq org-agenda-custom-commands
        '(("p" "Personal agenda"
           ((agenda "")
            (todo "TODO"
                  ((org-agenda-skip-function '(or (zb/org-skip-subtree-if-priority ?A)
                                                  (zb/org-skip-subtree-if-habit)))
                   (org-agenda-overriding-header "Other tasks:")))
            (todo "PENDING"
                  ((org-agenda-skip-function '(zb/org-skip-subtree-if-priority ?A))
                   (org-agenda-overriding-header "Pending:")))
            (todo "BLOCKED"
                  ((org-agenda-skip-function '(zb/org-skip-subtree-if-priority ?A))
                   (org-agenda-overriding-header "Blocked:")))))))
#+end_src

# ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
# ;; Org-Mode tweaks
# ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

# ;; Bind Org Capture to C-c c
# (global-set-key "\C-cc" 'org-capture)

# ;; (setq org-default-notes-file (concat "~/Nextcloud/GTD/inbox.org"))
# ;; (add-to-list 'org-capture-templates
# ;;              '("t" "To Do"  entry
# ;;                (file "~/Nextcloud/GTD/inbox.org")
# ;;                "* TODO %?" :empty-lines 1))

# ;; (add-to-list 'org-capture-templates
#              ;; '("w" "Work-related Task"  entry
#              ;;   (file "~/Nextcloud/GTD/work.org")
#              ;;   "* TODO %?" :empty-lines 1))


I consult my agenda pretty often, so I bind =C-c d= to open it a it faster. This
also copies any files I've sent through Drafts into my index file before
displaying the index, so they'll be in the agenda view, too.

#+begin_src emacs-lisp
  (defun zb/dashboard ()
    (interactive)
    (zb/copy-tasks-from-inbox)
    (find-file org-index-file)
    (org-agenda nil "p"))

  (global-set-key (kbd "C-c d") 'zb/dashboard)
#+end_src

*** Capturing tasks

Define a few common tasks as capture templates. Specifically, I frequently:

- Record ideas for future blog posts in =~/Documents/notes/blog-ideas.org=,
- Maintain a todo list in =~/Documents/org/index.org=.
- Convert emails into todos to maintain an empty inbox.

#+begin_src emacs-lisp
  (setq org-capture-templates
        '(("b" "Blog idea"
           entry
           (file "~/Documents/notes/blog-ideas.org")
           "* %?\n")

          ("c" "Contact"
           entry
           (file "~/Documents/contacts.org")
           "* %(org-contacts-template-name)
  :PROPERTIES:
  :ADDRESS: %^{123 Fake St., City, ST 12345}
  :PHONE: %^{555-555-5555}
  :EMAIL: %(org-contacts-template-email)
  :NOTE: %^{note}
  :END:")

          ("d" "Delivery" entry
           (file+headline "~/Documents/org/events.org" "Deliveries")
           "** %?\n   SCHEDULED: %t\n")

          ("e" "Email" entry
           (file+headline org-index-file "Inbox")
           "* TODO %?\n\n%a\n\n")

          ("f" "Finished book"
           table-line (file "~/Documents/notes/books-read.org")
           "| %^{Title} | %^{Author} | %u |")

          ("s" "Subscribe to an RSS feed"
           plain
           (file "~/Documents/rss-feeds.org")
           "*** [[%^{Feed URL}][%^{Feed name}]]")

          ("t" "Todo"
           entry
           (file+headline org-index-file "Inbox")
           "* TODO %?\n:PROPERTIES:\nCREATED: %u\n:END:\n")))
#+end_src

Refiling according to the document's hierarchy.

#+begin_src emacs-lisp
  (setq org-refile-use-outline-path t)
  (setq org-outline-path-complete-in-steps nil)
#+end_src

*** Keybindings

Bind a few handy keys.

#+begin_src emacs-lisp
  (define-key global-map "\C-cl" 'org-store-link)
  (define-key global-map "\C-ca" 'org-agenda)
  (define-key global-map "\C-cc" 'org-capture)
#+end_src

Hit =C-c i= to quickly open up my todo list.

#+begin_src emacs-lisp
  (defun zb/open-index-file ()
    "Open the master org TODO list."
    (interactive)
    (zb/copy-tasks-from-inbox)
    (find-file org-index-file)
    (flycheck-mode -1)
    (end-of-buffer))

  (global-set-key (kbd "C-c i") 'zb/open-index-file)
#+end_src

Hit =M-n= to quickly open up a capture template for a new todo.

#+begin_src emacs-lisp
  (defun org-capture-todo ()
    (interactive)
    (org-capture :keys "t"))

  (global-set-key (kbd "M-n") 'org-capture-todo)
  (add-hook 'gfm-mode-hook
            (lambda () (local-set-key (kbd "M-n") 'org-capture-todo)))
  (add-hook 'haskell-mode-hook
            (lambda () (local-set-key (kbd "M-n") 'org-capture-todo)))
#+end_src

Hit =C-c w= to quickly open up my work todo list.

#+begin_src emacs-lisp
  (defun zb/open-work-file ()
    "Open the work TODO list."
    (interactive)
    (find-file (org-file-path "work.org"))
    (flycheck-mode -1)
    (end-of-buffer))

  (global-set-key (kbd "C-c w") 'zb/open-work-file)
#+end_src

** Exporting

Translate regular ol' straight quotes to typographically-correct curly quotes
when exporting.

#+begin_src emacs-lisp
  (setq org-export-with-smart-quotes t)
#+end_src

**** Exporting to HTML

Don't include a footer with my contact and publishing information at the bottom
of every exported HTML document.

#+begin_src emacs-lisp
  (setq org-html-postamble nil)
#+end_src

**** Exporting to PDF

I want to produce PDFs with syntax highlighting in the code. The best way to do
that seems to be with the =minted= package, but that package shells out to
=pygments= to do the actual work. =pdflatex= usually disallows shell commands;
this enables that.

#+begin_src emacs-lisp
  (setq org-latex-pdf-process
        '("xelatex -shell-escape -interaction nonstopmode -output-directory %o %f"
          "xelatex -shell-escape -interaction nonstopmode -output-directory %o %f"
          "xelatex -shell-escape -interaction nonstopmode -output-directory %o %f"))
#+end_src

* TeX configuration

I rarely write LaTeX directly any more, but I often export through it with
org-mode, so I'm keeping them together.

# ;; TeX-engine options
# ;;   'xetex
# ;;   'xelatex
# ;;   'pdflatex

Always use =pdflatex= when compiling LaTeX documents. I don't really have any
use for DVIs.

#+begin_src emacs-lisp
  (setq TeX-PDF-mode t)
#+end_src

#+begin_src emacs-lisp
  (setq-default TeX-engine 'default)
  (setq TeX-parse-self t)
  ;; (latex-preview-pane-enable)
#+end_src

** Sciences Configuration
# #+begin_src emacs-lisp
#     (use-package auctex-lua)
#     (use-package auctex-latexmk)
#     (use-package textmate)
#     (use-package cdlatex)
#     (use-package latex-extra)
#     (use-package latex-math-preview)
#     (use-package latex-unicode-math-mode)
#     (use-package ebib)
# #+end_src


# Enable a minor mode for dealing with math (it adds a few useful keybindings),
# and always treat the current file as the "main" file. That's intentional, since
# I'm usually actually in an org document.

# #+begin_src emacs-lisp
#   (add-hook 'LaTeX-mode-hook
#             (lambda ()
#               (LaTeX-math-mode)
#               (setq TeX-master t)))
# #+end_src

* Editing settings

** Quickly visit Emacs configuration

I futz around with my dotfiles a lot. This binds =C-c e= to quickly open my
Emacs configuration file.

#+begin_src emacs-lisp
  (defun zb/visit-emacs-config ()
    (interactive)
    (find-file "~/.emacs.d/configuration.org"))

  (global-set-key (kbd "C-c e") 'zb/visit-emacs-config)
#+end_src

** Always kill current buffer

Assume that I always want to kill the current buffer when hitting =C-x k=.

#+begin_src emacs-lisp
  (defun zb/kill-current-buffer ()
    "Kill the current buffer without prompting."
    (interactive)
    (kill-buffer (current-buffer)))

  (global-set-key (kbd "C-x k") 'zb/kill-current-buffer)
#+end_src

** Look for executables in =/usr/local/bin=

#+begin_src emacs-lisp
  (zb/append-to-path "/usr/local/bin")
#+end_src

** Save my location within a file

Using =save-place-mode= saves the location of point for every file I visit. If I
close the file or close the editor, then later re-open it, point will be at the
last place I visited.

#+begin_src emacs-lisp
  (save-place-mode t)
#+end_src

** Always indent with spaces

Never use tabs

#+begin_src emacs-lisp
  (setq-default indent-tabs-mode nil)
#+end_src

** Configure =yasnippet=

#+begin_src emacs-lisp
  (use-package yasnippet)
#+end_src

I keep my snippets in =~/.emacs/snippets/text-mode=, and I always want =yasnippet=
enabled.

#+begin_src emacs-lisp
  (setq yas-snippet-dirs '("~/.emacs.d/snippets"))
;;  (setq yas-snippet-dirs '("~/.emacs.d/snippets/text-mode"))
  (yas-global-mode t)
#+end_src

I /don’t/ want =yas= to automatically indent the snippets it inserts. Sometimes
this looks pretty bad (when indenting org-mode, for example, or trying to guess
at the correct indentation for Python).

#+begin_src emacs-lisp
  (setq yas-indent-line 'auto)
#+end_src
** Switch and rebalance windows when splitting

When splitting a window, I invariably want to switch to the new window. This
makes that automatic.

#+begin_src emacs-lisp
  (defun zb/split-window-below-and-switch ()
    "Split the window horizontally, then switch to the new pane."
    (interactive)
    (split-window-below)
    (balance-windows)
    (other-window 1))

  (defun zb/split-window-right-and-switch ()
    "Split the window vertically, then switch to the new pane."
    (interactive)
    (split-window-right)
    (balance-windows)
    (other-window 1))

  (global-set-key (kbd "C-x 2") 'zb/split-window-below-and-switch)
  (global-set-key (kbd "C-x 3") 'zb/split-window-right-and-switch)
#+end_src

* Extra

# #+begin_src emacs-lisp
#   (load-file "~/.emacs-private.el")
# #+end_src

# ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
# ;; elfeed feed reader
# ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
#  (global-set-key (kbd "C-x w") 'elfeed)
#  (setq elfeed-feeds
#        '(("https://www.theverge.com/rss/index.xml" blog tech)
#  	("http://feeds.arstechnica.com/arstechnica/index" blog tech)
#  	("https://feeds.npr.org/1001/rss.xml" blog news)))
#  (setq-default elfeed-search-filter "@1-week-ago ")


#  use an org file to organise feeds
#  (use-package elfeed-org
#  (elfeed-org
#    :ensure t
#    :config
#    (elfeed-org)
#    (setq rmh-elfeed-org-files (list "~/Coding/rss_feeds.org")))

#  (setq rmh-elfeed-org-files (list "~/Coding/rss_feeds.org"))
#  shortcut functions
#  (defun bjm/elfeed-show-all ()
#    (interactive)
#    (bookmark-maybe-load-default-file)
#    (bookmark-jump "elfeed-all"))
#  (defun bjm/elfeed-show-emacs ()
#    (interactive)
#    (bookmark-maybe-load-default-file)
#    (bookmark-jump "elfeed-emacs"))
#  (defun bjm/elfeed-show-daily ()
#    (interactive)
#    (bookmark-maybe-load-default-file)
#    (bookmark-jump "elfeed-daily"))

#  (use-package elfeed
#    :ensure t
#    :bind (:map elfeed-search-mode-map
#                ("A" . bjm/elfeed-show-all)
#                ("E" . bjm/elfeed-show-emacs)
#                ("D" . bjm/elfeed-show-daily)
#                ("q" . bjm/elfeed-save-db-and-bury)))
